use std::str::FromStr;
use crate::ast::{Expr, Type, Declaration, Program, Interner};

grammar(interner: &mut Interner);

match {
    r"\s*" => { }, // The default whitespace skipping is disabled an `ignore pattern` is specified
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
} else {
    _
}

pub Program: Program = {
    <decls: DeclarationsAndMaybeExpr> => Program(decls.0, decls.1)
};

DeclarationList: Vec<Declaration> = {
    <list: DeclarationList> <decl: Declaration> => {
        let mut list = list;
        list.push(decl);
        list
    },
    <decl: Declaration> => vec![<>]
}

DeclarationsAndMaybeExpr: (Vec<Declaration>, Option<Expr>) = {
    <decls: DeclarationsAndExpr> => (decls.0, Some(decls.1)),
    <DeclarationList> => (<>, None)
}

DeclarationsAndExpr: (Vec<Declaration>, Expr) = {
    <Expr> => (vec![], <>),
    <list: DeclarationList> <expr: Expr> => (list, expr)
}

Declaration: Declaration = {
    <expr: Expr> ";" => Declaration::Expr(expr),
    "let" <var: Var> "=" <expr: Expr> ";" => Declaration::Let(interner.insert(var), expr),
    "letrec" <var: Var> ":" <ty: Type> "=" <expr: Expr> ";" => Declaration::LetRec(interner.insert(var), ty, expr),
    "type" <name: Var> "=" <ty: Type> ";" => Declaration::Type(interner.insert_type(name), ty)
}

Expr: Expr = {
    "fn" "(" <arg: Var> ":" <ty: Type> ")" "=>" <expr: Factor1> => Expr::Func { arg: interner.insert(arg), ty, body: Box::new(expr) },
    Factor1 => <>
};

Factor1: Expr = {
    "if" <test: Factor2> <then: Block> "else" <alt: Block> => Expr::If { test: Box::new(test), then: Box::new(then), alt: Box::new(alt) },
    Factor2 => <>
};

Factor2: Expr = {
    <op1: Factor2> "+" <op2: Factor3> => Expr::Plus(Box::new(op1), Box::new(op2)),
    <op1: Factor2> "-" <op2: Factor3> => Expr::Minus(Box::new(op1), Box::new(op2)),
    Factor3 => <>
}

Factor3: Expr ={
    <op1: Factor3> "*" <op2: Factor4> => Expr::Prod(Box::new(op1), Box::new(op2)),
    <op1: Factor3> "/" <op2: Factor4> => Expr::Div(Box::new(op1), Box::new(op2)),
    <op1: Factor3> "&&" <op2: Factor4> => Expr::And(Box::new(op1), Box::new(op2)),
    <op1: Factor3> "||" <op2: Factor4> => Expr::Or(Box::new(op1), Box::new(op2)),
    <op1: Factor3> "==" <op2: Factor4> => Expr::Eq(Box::new(op1), Box::new(op2)),
    Factor4 => <>,
}

Factor4: Expr ={
    "!" <expr: Atom> => Expr::Neg(Box::new(expr)),
    <expr: Atom> <index: r"\.[0-9]+"> => Expr::TupleAccess(Box::new(expr), usize::from_str(&index[1..]).unwrap()),
    <array: Var> "[" <index: Atom> "]" "|" <default: Atom> => Expr::ArrayAccess { array: interner.insert(array), index: index.into(), default: default.into() },
    Atom => <>,
}

Atom: Expr = {
    Int => Expr::Int(<>),
    String => Expr::String(<>),
    Bool => Expr::Bool(<>),
    Var => Expr::Var(interner.insert(<>)),
    <fun: Var> "(" <arg: Expr> ")" => Expr::Call(interner.insert(fun), Box::new(arg)),
    "(" <expr:Expr> ")" => expr,
    "(" <exprs:(<Expr> ",")+> <expr: Expr?> ")" => Expr::Tuple({
        let mut exprs = exprs;
        exprs.extend(expr.into_iter());
        exprs
    }),
    "()" => Expr::Unit,
    <Block> => <>,
    "[" <init: Expr> ";" <length: Expr> "]" => Expr::ImplicitArrayInit { init: Box::new(init), length: Box::new(length) },
    "[" <exprs: ("..."? Expr ",")+> "]" => Expr::ArrayInit {
        update: None,
        elements: exprs.into_iter().map(|(rest, ex, _)| (ex, rest.is_some())).collect()
    },
    "[" <exprs: ("..."? Expr ",")*> <tail: ("..."? Expr)> <update: (";" Expr "=" Expr)?> "]" => Expr::ArrayInit {
        update: update.map(|(_, index, _, value)| (index, value).into()),
        elements: exprs.into_iter()
            .map(|(rest, ex, _)| (rest, ex))
            .chain(std::iter::once(tail))
            .map(|(rest, ex)| (ex, rest.is_some()))
            .collect()
    },
}

Block: Expr = {
    "{" <decls: DeclarationsAndExpr> "}" => Expr::Block(decls.0, Box::new(decls.1))
}

Int: i64 = {
    r"-?[0-9]+" => i64::from_str(<>).unwrap()
};

Bool: bool = {
    "true" => true,
    "false" => false
};

String: String = {
    r###""[^"\n]+""### => (<>)[1..((<>).len() - 1)].to_owned()
};

Var: String = {
    r"[_a-zA-Z][A-Za-z0-9_]*" => (<>).to_owned()
};


Type: Type = {
    <ty: TypeAtom> "->" <ret: Type> => Type::Func(Box::new(ty), Box::new(ret)),
    <TypeAtom> "[]" => Type::Array(<>.into()),
    <TypeAtom> => <>
}


TypeAtom: Type = {
    "int" => Type::Int,
    "bool" => Type::Bool,
    "string" => Type::String,
    "()" => Type::Unit,
    <Var> => Type::Var(interner.insert_type(<>)),
    ParenthesizedType => <>
}

ParenthesizedType: Type = {
    "(" <ty: Type> ")" => ty,
    "(" <tys: (<Type> ",")+> <ty: Type?> ")" => Type::Tuple({
        let mut tys = tys;
        tys.extend(ty.into_iter());
        tys
    }),
}

TypeList: Vec<Type> = {
    <Type> => vec![<>],
    <list: TypeList> "," <ty: Type> => {
        let mut list = list;
        list.push(ty);
        list
    }  
}